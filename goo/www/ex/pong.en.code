; ball variables
ball.radius = 5
ball.x.start = screen.width / 2
ball.y.start = screen.height / 2
ball.x.min = player.width + ball.radius
ball.x.max = screen.width - player.width - ball.radius
ball.y.min = ball.radius
ball.y.max = screen.height - ball.radius
ball.vx := 1
ball.vy := random.bereik(-1,1)

; player variables
player.width = 10
player.height = 25
player.y.start = screen.height/2 - player.height/2
player.y.max = screen.height - player.height

; ball movement
ball.x := ball.x.start
ball.y := ball.y.start

if ¬ pause and (ball.vx > 0 or ball.vx < 0) then
 ball.x := ball.x + ball.vx
end

if ¬ pause and (ball.vy > 0 or ball.vy < 0) then
 ball.y := ball.y + ball.vy
end

; pause
pause.til := runtime + 1
pause = runtime < pause.til



; BOUNCE

; hits ball the players?
right.hits = (ball.y > right.y and ball.y < right.y + player.height)
left.hits = (ball.y > left.y and ball.y < left.y + player.height)

; right bounce
ball.x.min = 0 + ball.radius + player.width
ball.x.max = screen.width - ball.radius - player.width

if ball.x > ball.x.max and right.hits then
 ball.x := ball.x.max
 ball.vx := -2
 ball.vy := -(right.y - ball.y + player.height/2) / 4
end

; left bounce
if ball.x < ball.x.min and left.hits then
 ball.x := ball.x.min
 ball.vx := - ball.vx
 ball.vy := -(left.y - ball.y + player.height/2) / 4
end

; top and below bounce
if ball.y < ball.y.min then
 ball.vy := abs ball.vy
end
if ball.y > ball.y.max then
 ball.vy := - abs ball.vy
end


; score
score1 := 0
score2 := 0

if ball.x < 0 or ball.x > screen.width then
 ball.x := 90
 ball.y := 50
 pause.til := runtime + 1
end

if ball.x < 0              then score1 := score1 + 1 end
if ball.x > screen.width then score2 := score2 + 1 end



; players
left.y := player.y.start
right.y := player.y.start

; controls
if key.down and right.y > 0 then right.y := right.y - 2 end
if key.up and right.y < player.y.max then right.y := right.y + 2 end
if key.s and left.y > 0 then left.y := left.y - 2 end
if key.w and left.y < player.y.max then left.y := left.y + 2 end
 
; draw players
left.min = (0, left.y)
left.max = (player.width, left.y + player.height)
right.min = (screen.width - player.width, right.y)
right.max = (screen.width, right.y + player.height)

right = rectangle(right.min, right.max)
left = rectangle(left.min, left.max)

; draw ball
ball = circle((ball.x,ball.y), ball.radius)

label1 = label((90-9,90), string(score1))
label2 = label((90+3,90), string(score2))

out = (ball.vx,ball.vy ⇒ (draw [ left, right, ball, label1, label2 ]))

