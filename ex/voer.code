;;;;;;;;;;;;;;;;;;;; SPEL LOGICA ;;;;;;;;;;;;;;;;;;;;

bord := [0]^42
aanzet := ja
status := "aan zet"
klaar := nee

ckleur = palet(bord(y·7+x))
palet  = [kleur.zwart, kleur.rood, kleur.geel, kleur.groen]

; kan de zet?
kanzet = kbord,kx → (kbord(kx + 7·5) = 0)
doezet = dbord,dx,dkleur → dbord0
als kanzet(dbord,dx) dan
	dbord0 = newindex(dbord, dx + dy·7, dkleur)
anders
	dbord0 = newindex(dbord, 0, 3)
eind

dy = (|)[ (dbord(dx)=0 ⇒ 0), (dbord(dx+7)=0 ⇒ 1), (dbord(dx+14)=0 ⇒ 2), (dbord(dx+21)=0 ⇒ 3), (dbord(dx+28)=0 ⇒ 4), (dbord(dx+35)=0 ⇒ 5) ]

als aanzet dan
	kleur = 1
anders
	kleur = 2
eind

als wint(bord,1) dan
	status := "rood heeft gewonnen"
	klaar := ja
eind
als wint(bord,2) dan
	status := "geel heeft gewonnen"
	klaar := ja
eind


; wint er iemand ?
wint = wbord,ws → (hwint of vwint of dwint of rwint)

hindices = (0..4 × (0..6 · 7)) map (+)
vindices = (0..7 × (0..3 · 7)) map (+)
dindices = (0..4 × (0..3 · 7)) map (+)
rindices = (3..7 × (0..3 · 7)) map (+)

hwint = ⋁ hindices map hwint0
vwint = ⋁ vindices map vwint0
dwint = ⋁ dindices map dwint0
rwint = ⋁ rindices map rwint0

hwint0 = hs → (wbord(hs)=ws en wbord(hs+1)=ws en wbord(hs+2)=ws en wbord(hs+3)=ws)
vwint0 = vs → (wbord(vs)=ws en wbord(vs+7)=ws en wbord(vs+14)=ws en wbord(vs+21)=ws)
dwint0 = ds → (wbord(ds)=ws en wbord(ds+8)=ws en wbord(ds+16)=ws en wbord(ds+24)=1)
rwint0 = rs → (wbord(rs)=ws en wbord(rs+6)=ws en wbord(rs+12)=ws en wbord(rs+18)=1)

;;;;;;;;;;;;;;;;;;;;  GUI  ;;;;;;;;;;;;;;;;;;;;

; speler dropt coin
als ¬ klaar en muis.klik.begin en aanzet dan
	bord := doezet(bord, xsnap, 1)
	status := "denken..."
	aanzet := nee
	nadenktot := nu + 0.1
	prev := nu + 0.1
eind


; computer dropt coin
klaar := nee
nadenktot := nu
bz := 0
prev := nu
denktijd := 0

als ¬ klaar en ¬ aanzet en nu > nadenktot dan
	;scores := bscores
	bord := doezet(bord, bestezet(bord,2), 2)
	status := "aan zet"
	aanzet := ja
	denktijd := klok(X → bestezet(bord,2))
	;scores := (0..7 × 0..7) map (I0,J0 → score(Bbord1)) 
eind

;Bbord1 = doezet(doezet(bbord,I0,2),J0,1)



;;;;;;;;;;;;;;;;;;;;  AI  ;;;;;;;;;;;;;;;;;;;;

; beste zet berekenen
bestezet = bbord,bspeler → bzet

scores = (0..7) map (ii → score(bbord,ii))
bzet = maxindex scores

;scoorzet2 = (I → score(doezet(bord,I,2),2))
;scoorzet1 = (J → 1000-score(doezet(bord,J,1),1))

; score berekenen
score = sbord,szet → predefscores(szet)

predefscores = [3, 4, 5, 7, 5, 4, 3]



;;;;;;;;;;;;;;;;;;;;  TEKENEN  ;;;;;;;;;;;;;;;;;;;;

xmod  = ⌊(muis.x-30) / 15⌋
xsnap = klem(xmod, 0, 6)
xdraw  = 30 + xsnap · 15 + 7.5

; teken
blauwbord = rechthoek((29,0),(135,90))   verf kleur.blauw
selectie  = cirkel(xdraw, 95, 3)     verf (palet(2 - kleur))
stuk      = cirkel(x·15+37,y·15+7,7) verf ckleur
stukken   = (0..7 × 0..6) map (x,y → stuk)

statuslabel = label(0,90,status)
infolabel1  = label(0,80,"wint(1)   = " ‖ tekst(wint(bord,1)))
infolabel2  = label(0,75,"wint(2)   = " ‖ tekst(wint(bord,2)))
infolabel3  = label(0,70,"score     = " ‖ tekst(score(bord,0)))
infolabel4  = label(0,65,"denktijd  = " ‖ tekst(denktijd))

uit = teken ([blauwbord] ‖ stukken ‖ [selectie, statuslabel, infolabel1, infolabel2, infolabel3, infolabel4])










