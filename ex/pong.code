; Pong Deluxe

; bal variabelen
bal.x.min = 15
bal.y.min = 5
bal.x.max = uit.breedte - 15
bal.y.max = uit.hoogte - 5
bal.vx := 1
bal.vy := willekeurig(-1,1)

; speler variabelen
speler.y.start = speler.y.max / 2
speler.y.max = uit.hoogte - 25

; bal verplaatsing
bal.x := uit.breedte / 2
bal.y := uit.hoogte / 2

; pauze
pauze.tot := looptijd + 1
pauze = looptijd < pauze.tot

als scherm.ververst en Â¬ pauze dan
 bal.x := bal.x + bal.vx
 bal.y := bal.y + bal.vy
eind

; raakt bal de spelers?
rechts.raakt = (bal.y > rechts.y en bal.y < rechts.y + 25)
links.raakt = (bal.y > links.y en bal.y < links.y + 25)

; rechts stuiter
bal.x.min = 0 + 5 + 10
bal.x.max = uit.breedte - 5 - 10

als bal.x > bal.x.max en rechts.raakt dan
 bal.x := bal.x.max
 bal.vx := - abs bal.vx
 bal.vy := -(rechts.y - bal.y + 25/2) / 8
eind

; links stuiter
als bal.x < bal.x.min en links.raakt dan
 bal.x := bal.x.min
 bal.vx := abs bal.vx
 bal.vy := -(links.y - bal.y + 25/2) / 8
eind

; top en onder stuiter
als bal.y < bal.y.min dan
 bal.vy := abs bal.vy
eind
als bal.y > bal.y.max dan
 bal.vy := - abs bal.vy
eind


; score
score1 := 0
score2 := 0

; er wordt gescoord
als bal.x < 0 of bal.x > uit.breedte dan
	bal.x := 90
	bal.y := 50
	pauze.tot := looptijd + 1
eind
als bal.x < 0              dan score1 := score1 + 1 eind
als bal.x > uit.breedte dan score2 := score2 + 1 eind

; spelers
links.y := speler.y.start
rechts.y := speler.y.start

; besturing
als scherm.ververst en toets.omlaag en rechts.y > 0						dan rechts.y := rechts.y - 2 eind
als scherm.ververst en toets.omhoog en rechts.y < speler.y.max dan rechts.y := rechts.y + 2 eind
als scherm.ververst en toets.s en links.y > 0									dan links.y := links.y - 2 eind
als scherm.ververst en toets.w en links.y < speler.y.max				dan links.y := links.y + 2 eind
 
; teken spelers
links.min = (0, links.y)
links.max = (10, links.y + 25)
rechts.min = (uit.breedte - 10, rechts.y)
rechts.max = (uit.breedte, rechts.y + 25)

rechts = rechthoek(rechts.min, rechts.max)
links = rechthoek(links.min, links.max)

; teken bal
bal = cirkel((bal.x, bal.y), 5)

label1 = label((90-9, 90), tekst(score2))
label2 = label((90-2, 90), "-")
label3 = label((90+3, 90), tekst(score1))

uit = teken [ links, rechts, bal, label1, label2, label3 ]

