; Pong Deluxe

; ball variables
ball.x.min = 15
ball.y.min = 5
ball.x.max = screen.width - 15
ball.y.max = screen.height - 5
ball.vx := 3
ball.vy := random(-3,3)

; player variables
player.y.start = player.y.max / 2
player.y.max = screen.height - 25

; ball movement
ball.x := screen.width / 2
ball.y := screen.height / 2

; pause
pause.til := runtime + 1
pause = runtime < pause.til

if screen.refreshes and Â¬ pause then
 ball.x := ball.x + ball.vx
 ball.y := ball.y + ball.vy
end

; hits ball the players?
right.hits = (ball.y > right.y and ball.y < right.y + 25)
left.hits = (ball.y > left.y and ball.y < left.y + 25)

; right bounce
ball.x.min = 0 + 5 + 10
ball.x.max = screen.width - 5 - 10

if ball.x > ball.x.max and right.hits then
 ball.x := ball.x.max
 ball.vx := - abs ball.vx
 ball.vy := -(right.y - ball.y + 25/2) / 8
end

; left bounce
if ball.x < ball.x.min and left.hits then
 ball.x := ball.x.min
 ball.vx := abs ball.vx
 ball.vy := -(left.y - ball.y + 25/2) / 8
end

; top and below bounce
if ball.y < ball.y.min then
 ball.vy := abs ball.vy
end
if ball.y > ball.y.max then
 ball.vy := - abs ball.vy
end


; score
score1 := 0
score2 := 0

; er wordt gescoord
if ball.x < 0 or ball.x > screen.width then
	ball.x := 90
	ball.y := 50
	pause.til := runtime + 1
end
if ball.x < 0              then score1 := score1 + 1 end
if ball.x > screen.width then score2 := score2 + 1 end

; players
left.y := player.y.start
right.y := player.y.start

; controls
if screen.refreshes and key.down and right.y > 0						then right.y := right.y - 2 end
if screen.refreshes and key.up and right.y < player.y.max then right.y := right.y + 2 end
if screen.refreshes and key.s and left.y > 0									then left.y := left.y - 2 end
if screen.refreshes and key.w and left.y < player.y.max				then left.y := left.y + 2 end
 
; draw players
left.min = (0, left.y)
left.max = (10, left.y + 25)
right.min = (screen.width - 10, right.y)
right.max = (screen.width, right.y + 25)

right = rectangle(right.min, right.max)
left = rectangle(left.min, left.max)

; draw ball
ball = circle((ball.x, ball.y), 5)

label1 = label((90-9, 90), string(score2))
label2 = label((90-2, 90), "-")
label3 = label((90+3, 90), string(score1))

out = draw [ left, right, ball, label1, label2, label3 ]

