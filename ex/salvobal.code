; Salvobal!

hw = screen.width / 2

left.x  := screen.width · 1 / 4
right.x := screen.width · 3 / 4
left.y  := 0
right.y := 0

left.vx  := 0
left.vy  := 0
right.vx := 0
right.vy := 0

; ball pos
ball.x := hw
ball.y := 50
ball.vx := 0
ball.vy := 0

pause.til := runtime + 1
pause = runtime < pause.til


; springen
if key.w.begin and left.y = 0 then
	left.vy := 2
	left.y := 2
end

if key.up.begin and right.y = 0 then
	right.vy := 2
	right.y := 2
end

; player beweging
if screen.refreshes and ¬ pause and ball.y > 0 then
	; left
	if key.a and left.x > 0 then
		left.x := left.x - 1
	end

	if key.d and left.x < hw - 10 then
		left.x := left.x + 1
	end

	; spring
	if left.y > 0 then
		left.y := left.y + left.vy
		left.vy := left.vy - .05
	end
	if left.y < 0 then
		left.y := 0
	end

	; right
	if key.left and right.x > hw+10 then
		right.x := right.x - 1
	end

	if key.right and right.x < hw · 2 then
		right.x := right.x + 1
	end

	; spring
	if right.y > 0 then
		right.y := right.y + right.vy
		right.vy := right.vy - .05
	end
	if right.y < 0 then
		right.y := 0
	end
	
	; ball update
	if mouse.click then
		ball.x := mouse.x
		ball.y := mouse.y
		ball.vx := 0
		ball.vy := 0
	end

	; ball physics
	if ball.x < 0 then
		ball.vx := abs ball.vx
	end
	
	if ball.y > 200 then
		ball.y := 200
		ball.vy := 0
	end

	
	if ball.x > screen.width then
		ball.vx := - abs ball.vx
	end
	
	if ldst < 18 then 
		ball.x := left.x + lnx·18.1
		ball.y := left.y + lny·18.1
		ball.vx := ball.vx - 2·ldot·lnx
		ball.vy := ball.vy - 2·ldot·lny
	end
	
	if rdst < 18 then
		ball.x := right.x + rnx·18.1
		ball.y := right.y + rny·18.1
		ball.vx := ball.vx - 2·rdot·rnx
		ball.vy := ball.vy - 2·rdot·rny
	end
	
	if ldst > 18 and rdst > 18 then
		ball.x := ball.x + ball.vx
		ball.y := ball.y + ball.vy
		ball.vy := ball.vy - .05
	end

end

; bounce left
ldx = ball.x - left.x
ldy = ball.y - left.y
ldst = √(ldx²+ldy²)
lnx = ldx/ldst
lny = ldy/ldst
ldot = ball.vx·lnx+ball.vy·lny



; bounce right
rdx = ball.x - right.x
rdy = ball.y - right.y
rdst = √(rdx²+rdy²)
rnx = rdx/rdst
rny = rdy/rdst
rdot = ball.vx·rnx+ball.vy·rny

; score
score1 := 0
score2 := 0
fk := color.black

if fk = color.black then
	flits = rectangle((0,0),(0,0))
else
	flits = paint(rectangle((0,0),(screen.width,screen.height)), fk)
end

if screen.refreshes and ball.y > 0 then
	fk := rgb(max(fk₀-.015,0), max(fk₁-.015,0),max(fk₂-.015,0))
end

; gescoord
if ball.y < 0 then 
	ball.y := 50
	ball.vx := 0
	ball.vy := 0

	if ball.x < screen.width / 2 then
		score1 := score1 + 1
		ball.x := hw / 2 · 3
		fk := color.blue
	end

	if ball.x > screen.width / 2 then
		score2 := score2 + 1
		ball.x := hw / 2
		fk := color.yellow
	end

end



; draw
left  = arc(left.x,left.y, 10, τ/2, τ)   paint color.yellow
right = arc(right.x,right.y, 10, τ/2, τ) paint color.blue
net    = rectangle((hw-1,0),(hw+1,10))       paint color.white
ball    = circle(ball.x, ball.y, 8)             paint balkleur
label1 = label(hw-5,90, string(score1))
label2 = label(hw+5,90, string(score2))

ball.f = √(ball.vx² + ball.vy²) / 10
balkleur = rgb(1 - ball.f, ball.f, 0)

out = draw [ flits, left, right, net, ball, label1, label2 ]





