; Salvobal!

hw = scherm.breedte / 2

links.x  := scherm.breedte · 1 / 4
rechts.x := scherm.breedte · 3 / 4
links.y  := 0
rechts.y := 0

links.vx  := 0
links.vy  := 0
rechts.vx := 0
rechts.vy := 0

; bal pos
bal.x := hw
bal.y := 50
bal.vx := 0
bal.vy := 0

pauze.tot := looptijd + 1
pauze = looptijd < pauze.tot


; springen
als toets.w.begin en links.y = 0 dan
	links.vy := 2
	links.y := 2
eind

als toets.omhoog.begin en rechts.y = 0 dan
	rechts.vy := 2
	rechts.y := 2
eind

; speler beweging
als scherm.ververst en ¬ pauze en bal.y > 0 dan
	; links
	als toets.a en links.x > 0 dan
		links.x := links.x - 1
	eind

	als toets.d en links.x < hw - 10 dan
		links.x := links.x + 1
	eind

	; spring
	als links.y > 0 dan
		links.y := links.y + links.vy
		links.vy := links.vy - .05
	eind
	als links.y < 0 dan
		links.y := 0
	eind

	; rechts
	als toets.links en rechts.x > hw+10 dan
		rechts.x := rechts.x - 1
	eind

	als toets.rechts en rechts.x < hw · 2 dan
		rechts.x := rechts.x + 1
	eind

	; spring
	als rechts.y > 0 dan
		rechts.y := rechts.y + rechts.vy
		rechts.vy := rechts.vy - .05
	eind
	als rechts.y < 0 dan
		rechts.y := 0
	eind
	
	; bal update
	als muis.klik dan
		bal.x := muis.x
		bal.y := muis.y
		bal.vx := 0
		bal.vy := 0
	eind

	; bal physics
	als bal.x < 0 dan
		bal.vx := abs bal.vx
	end
	
	als bal.y > 200 dan
		bal.y := 200
		bal.vy := 0
	eind

	
	als bal.x > scherm.breedte dan
		bal.vx := - abs bal.vx
	eind
	
	als ldst < 18 dan 
		bal.x := links.x + lnx·18.1
		bal.y := links.y + lny·18.1
		bal.vx := bal.vx - 2·ldot·lnx
		bal.vy := bal.vy - 2·ldot·lny
	eind
	
	als rdst < 18 dan
		bal.x := rechts.x + rnx·18.1
		bal.y := rechts.y + rny·18.1
		bal.vx := bal.vx - 2·rdot·rnx
		bal.vy := bal.vy - 2·rdot·rny
	eind
	
	als ldst > 18 en rdst > 18 dan
		bal.x := bal.x + bal.vx
		bal.y := bal.y + bal.vy
		bal.vy := bal.vy - .05
	eind

eind

; stuiter links
ldx = bal.x - links.x
ldy = bal.y - links.y
ldst = √(ldx²+ldy²)
lnx = ldx/ldst
lny = ldy/ldst
ldot = bal.vx·lnx+bal.vy·lny



; stuiter rechts
rdx = bal.x - rechts.x
rdy = bal.y - rechts.y
rdst = √(rdx²+rdy²)
rnx = rdx/rdst
rny = rdy/rdst
rdot = bal.vx·rnx+bal.vy·rny

; score
score1 := 0
score2 := 0
fk := kleur.zwart

als fk = kleur.zwart dan
	flits = rechthoek((0,0),(0,0))
anders
	flits = verf(rechthoek((0,0),(scherm.breedte,scherm.hoogte)), fk)
eind

als scherm.ververst en bal.y > 0 dan
	fk := rgb(max(fk₀-.015,0), max(fk₁-.015,0),max(fk₂-.015,0))
end

; gescoord
als bal.y < 0 dan 
	bal.y := 50
	bal.vx := 0
	bal.vy := 0

	als bal.x < scherm.breedte / 2 dan
		score1 := score1 + 1
		bal.x := hw / 2 · 3
		fk := kleur.blauw
	eind

	als bal.x > scherm.breedte / 2 dan
		score2 := score2 + 1
		bal.x := hw / 2
		fk := kleur.geel
	eind

eind



; teken
links  = boog(links.x,links.y, 10, τ/2, τ)   verf kleur.geel
rechts = boog(rechts.x,rechts.y, 10, τ/2, τ) verf kleur.blauw
net    = rechthoek((hw-1,0),(hw+1,10))       verf kleur.wit
bal    = cirkel(bal.x, bal.y, 8)             verf balkleur
label1 = label(hw-5,90, tekst(score1))
label2 = label(hw+5,90, tekst(score2))

bal.f = √(bal.vx² + bal.vy²) / 10
balkleur = rgb(1 - bal.f, bal.f, 0)

uit = teken [ flits, links, rechts, net, bal, label1, label2 ]





